= Exercise 16
== Exercise 16: Importing data

=== Preparations

Make sure you have the Movie database, which contains 175 nodes and 257 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/AfterExercise14.png[After exercise 14,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/AfterExercise14.cypher[]
----


== Exercise Overview

In this exercise you write Cypher statements to import data into your graph.

. In Part 1, you import actor data from a CSV file.
. In Part 2, you import movie data from a CSV file.
. In Part 3, you import data to relate actors to movies from a CSV file.

Go to the next page to perform the three parts of this exercise.

== Instructions, Exercise 16, Part 1
.*Importing actor data*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/actors.csv that you must load into your graph.

. Write the Cypher statement to return the data from the file.
. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.
. Write the Cypher statement to load the data into your graph.
*Hint:* Use `MERGE` because the graph already contains some of these actors.

== Solution, Exercise 16, Part 1
.*Importing actor data*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/actors.csv that you must load into your graph.

*1. Write the Cypher statement to return the data from the file.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/actors.csv'
AS line
RETURN line.id, line.name, line.birthYear
----

The result should be:

[.thumb]
image::{guides}/img/LoadActorsToView.png[LoadActorsToView,width=600]

*2. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/actors.csv'
AS line
RETURN line.id, line.name, toInteger(trim(line.birthYear))
----

The result should be:

[.thumb]
image::{guides}/img/LoadAndFormatActorData.png[LoadAndFormatActorData,width=600]

*3. Write the Cypher statement to load the data into your graph.*

*Hint:* Use `MERGE` because the graph already contains some of these actors.

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/actors.csv'
AS line
MERGE (actor:Person {name: line.name})
  ON CREATE SET actor.born = toInteger(trim(line.birthYear)), actor.actorId = line.id
  ON MATCH SET actor.actorId = line.id
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadedActors.png[LoadedActors,width=600]

Next, Part 2

== Instructions, Exercise 16, Part 2
.*Importing movie data*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/movies.csv that you must load into your graph.

. Write the Cypher statement to return the data from the file.
. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.
. Write the Cypher statement to load the data into your graph.
*Hint:* Use `MERGE` because the graph already contains some of these movies.

== Solution, Exercise 16, Part 2
.*Importing movie data*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/movies.csv that you must load into your graph.

.*1. Write the Cypher statement to return the data from the file.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/movies.csv'
AS line
RETURN line.id, line.title, line.year, line.tagLine
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadMoviesToView.png[LoadMoviesToView,width=600]

*2. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/movies.csv'
AS line
RETURN line.id, line.title, toInteger(line.year), trim(line.tagLine)
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadAndFormatMovies.png[LoadAndFormatMovies,width=600]

*3. Write the Cypher statement to load the data into your graph.*

*Hint:* Use `MERGE` because the graph already contains some of these movies.

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/movies.csv'
AS line
MERGE (m:Movie {title: line.title})
ON CREATE 
  SET m.released = toInteger(trim(line.year)), 
      m.movieId = line.id,
      m.tagline = line.tagLine
  ON MATCH SET m.movieId = line.id
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadedMovies.png[LoadedMovies,width=600]

Next, Part 3

== Instructions, Exercise 16, Part 3
.*Importing data to relate actors to movies*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/roles.csv that you must load into your graph.

. Write the Cypher statement to return the data from the file.
. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.
. Write the Cypher statement to load the data into your graph.

*Hint:* Use `MERGE` because the graph already contains one of these relationships.

== Solution, Exercise 16, Part 3
.*Importing data to relate actors to movies*

You are given the name of a file, http://data.neo4j.com/intro-neo4j/roles.csv that you must load into your graph.

*1. Write the Cypher statement to return the data from the file.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/roles.csv'
AS line FIELDTERMINATOR ';'
RETURN line.personId, line.movieId, line.Role
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadRolesToView.png[LoadRolesToView,width=600]


*2. Write the Cypher statement to read the data and return it, ensuring that the data returned is properly formatted.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/roles.csv'
AS line FIELDTERMINATOR ';'
RETURN line.personId, line.movieId, split(line.Role,',')
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadAndFormatRoles.png[LoadAndFormatRoles,width=600]

*3. Write the Cypher statement to load the data into your graph.*

[source, cypher]
----
LOAD CSV WITH HEADERS
FROM 'http://data.neo4j.com/intro-neo4j/roles.csv'
AS line FIELDTERMINATOR ';'
MATCH (movie:Movie { movieId: line.movieId })
MATCH (person:Person { actorId: line.personId })
MERGE (person)-[:ACTED_IN { roles: split(line.Role,',')}]->(movie)
----

The result returned should be:

[.thumb]
image::{guides}/img/LoadedRoles.png[LoadedRoles,width=600]


== Exercise Summary

In this exercise you wrote Cypher statements to load data that is properly formatted into the graph.

