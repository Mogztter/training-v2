= Exercise 7
== Exercise 7: Working with Cypher data

=== Preparations

Make sure you have the Movie database, which contains 171 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/InitialDatabase.png[InitialDatabase,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/initialDatabase.cypher[]
----


== Exercise Overview

In the previous exercises, you wrote queries using Cypher syntax for retrieving, filtering, ordering, avoiding duplication, and performing intermediate processing during a query.
These queries give you a good basis for exploring more types of queries against the _Movie_ database you are working with for this training.

In this exercise, you will write more Cypher statements to work with lists and you will also work with dates.
First, you will write a query where lists are collected and the results returned are sorted by the size of the list.
Then you will write a query that collects and unwinds a list.
Finally, you write a query that performs calculations based upon a date retrieved from the graph.

. In Part 1, you collect and use lists.
. In Part 2, you collect and then unwind a list.
. In Part 3, you perform a calculation with the date type.

Go to the next page to perform the three parts of this exercise.

== Instructions, Exercise 7, Part 1
.*Collecting and using lists*

Write a Cypher query that retrieves all actors that acted in movies, and also retrieves the producers for those movies.
During the query, collect the names of the actors and the names of the producers.
Returns the movie titles, along with the list of actors for each movie, and the list of producers for each movie making sure there is no duplication of data.
Order the results returned based upon the size of the list of actors.

*Hints:* Use two patterns for the query, one for actors, and one for producers.
Use a `WITH` clause to collect the values for the cast and producers.
You can use the `size()` function to calculate the size of a list for ordering purposes.

== Solution, Exercise 7, Part 1
.*Collecting and using lists*

*Write a Cypher query that retrieves all actors that acted in movies, and also retrieves the producers for those movies.
During the query, collect the names of the actors and the names of the producers.
Returns the movie titles, along with the list of actors for each movie, and the list of producers for each movie making sure there is no duplication of data.
Order the results returned based upon the size of the list of actors.*

*Hints:* Use two patterns for the query, one for actors, and one for producers.
Use a `WITH` clause to collect the values for the cast and producers.
You can use the `size()` function to calculate the size of a list for ordering purposes.

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie),
      (m)<-[:PRODUCED]-(p:Person)
WITH  m, collect(a.name) AS cast, collect(DISTINCT p.name) AS producers
RETURN DISTINCT m.title, cast, producers
ORDER BY size(cast)
----

[.thumb]
image::{guides}/img/UsingLists.png[UsingLists,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 2

== Instructions, Exercise 7, Part 2
.*Unwinding lists*

. Write a Cypher query that retrieves all actors that acted in movies, and collects the list of movies for any actor that acted in more than five movies.
Return the name of the actor and the list.
. Modify the query you just wrote so that before the query processing ends, you unwind the list of movies and then return the name of the actor and the title of the associated movie.

*Hints:* Use two `WITH` clauses and test the size of the collected list using the `size()` function.


== Solution, Exercise 7, Part 2
.*Collecting and using lists*

*1. Write a Cypher query that retrieves all actors that acted in movies, and collects the list of movies for any actor that acted in more than five movies.
Return the name of the actor and the list.*

*Hints:* Use two `WITH` clauses and test the size of the collected list using the `size()` function.

[source, cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WITH p, collect(m) AS movies
WHERE size(movies)  > 5
RETURN p.name, movies
----

[.thumb]
image::{guides}/img/ActorsWithMoreThan5Movies1.png[ActorsWithMoreThan5Movies1,width=400,float="right"]

The result returned should be:

In this view, we are examining the table view where we can see the actor name and the list of movies for each actor.
The actors should be _Tom Hanks_ and _Keanu Reeves_.

++++
<div style="clear: both;"></div>
++++

*2. Modify the query you just wrote so that before the query processing ends, you unwind the list of movies and then return the name of the actor and the title of the associated movie.*

*Hints:* Use two `WITH` clauses and test the size of the collected list using the `size()` function.

[source, cypher]
----
MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WITH p, collect(m) AS movies
WHERE size(movies)  > 5
WITH p, movies UNWIND movies AS movie
RETURN p.name, movie.title
----

[.thumb]
image::{guides}/img/ActorsWithMoreThan5Movies2.png[ActorsWithMoreThan5Movies2,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 3

== Instructions, Exercise 7, Part 3
.*Calculating with dates.*

Write a query that retrieves all movies that _Tom Hanks_ acted in, returning the title of the movie, the year the movie was released, the number of years ago that the movie was released, and the age of _Tom_ when the movie was released.

== Solution, Exercise 7, Part 3
.*Calculating with dates.*

*Write a query that retrieves all movies that _Tom Hanks_ acted in, returning the title of the movie, the year the movie was released, the number of years ago that the movie was released, and the age of _Tom_ when the movie was released.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE a.name = 'Tom Hanks'
RETURN  m.title, m.released, date().year  - m.released as yearsAgoReleased, m.released  - a.born AS `age of Tom`
ORDER BY yearsAgoReleased
----

[.thumb]
image::{guides}/img/AgeCalculations.png[AgeCalculations,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++


== Exercise Summary

In this exercise, you wrote more Cypher statements to work with lists and dates.
First, you wrote a query where lists are collected and the results returned are sorted by the size of the list.
Then you wrote a query that collects and unwinds a list.
Finally, you wrote a query that performs calculations based upon a date retrieved from the graph.

pass:a[<a play-topic='{guides}/08.html'>Continue to Exercise 8</a>]
