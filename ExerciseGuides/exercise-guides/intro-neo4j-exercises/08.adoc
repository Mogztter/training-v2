= Exercise 8
== Exercise 8: Creating Nodes

=== Preparations

Make sure you have the Movie database, which contains 171 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/InitialDatabase.png[InitialDatabase,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/initialDatabase.cypher[]
----


== Exercise Overview

In the previous exercises, you have gained experience with Cypher queries.

In this exercise, you will write Cypher statements to create nodes.
Then you will write Cypher statements to add and remove labels for nodes.
Next, you will use Cypher to add properties to nodes.
Finally, you will write Cypher statements to remove properties from nodes.

. In Part 1, you create nodes.
. In Part 2, you modify the labels for nodes.
. In Part 3, you add properties to nodes.
. In Part 4, you remove properties from nodes.

Go to the next page to perform the four parts of this exercise.

== Instructions, Exercise 8, Part 1
.*Creating nodes*

Write Cypher statements to:

. Create a _Movie_ node for the movie with the _title_, _Forrest Gump_.
. Retrieve the node you just created by its _title_.
. Create a _Person_ node for the person with the _name_, _Robin Wright_.
. Retrieve the _Person_ node you just created by its _name_.


== Solution, Exercise 8, Part 1
.*Creating nodes*

Write Cypher statements to:

*1. Create a _Movie_ node for the movie with the _title_, _Forrest Gump_.*

[source, cypher]
----
CREATE (:Movie {title: 'Forrest Gump'})
----

[.thumb]
image::{guides}/img/CreateForrestGump.png[CreateForrestGump,width=300,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Retrieve the node you just created by its _title_.*

[source, cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN m
----

[.thumb]
image::{guides}/img/RetrieveForrestGump.png[RetrieveForrestGump,width=400,float="right"]

The result returned should be:

*Note:* The value for the _id_ property for the newly created node may be different for your graph.
It this screen shot, it is _741_.

++++
<div style="clear: both;"></div>
++++

*3. Create a _Person_ node for the person with the _name_, _Robin Wright_.*

[source, cypher]
----
CREATE (:Person {name: 'Robin Wright'})
----

[.thumb]
image::{guides}/img/CreateRobinWright.png[CreateRobinWright,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*5. Retrieve the _Person_ node you just created by its _name_.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p
----

[.thumb]
image::{guides}/img/RetrieveRobinWright.png[RetrieveRobinWright,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 2

== Instructions, Exercise 8, Part 2
.*Modifying node labels*

Next, you will practice modifying node labels.
Node labels are important because they are used to quickly retrieve groups of nodes.

Write Cypher statements to do the following:

. Add the label _OlderMovie_ to any _Movie_ node that was released before 2010.
. Retrieve all older movie nodes to test that the label was indeed added to these nodes.
. Add the label _Female_ to all _Person_ nodes that has a person whose name starts with _Robin_.
. Retrieve all _Female_ nodes.
Do you notice that not all of these nodes should be labeled _Female_?
. We've decided to not use the _Female_ label.
Remove the _Female_ label from the nodes that have this label.
. View the current schema of the graph.

.*Taking it further (optional)*
** Add more labels to the _Movie_ nodes to reflect the movie genre (action, drama, etc.).
** Query the database using different labels for movies.

== Solution, Exercise 8, Part 2
.*Modifying node labels*

Write Cypher statements to do the following:

*1. Add the label _OlderMovie_ to any _Movie_ node that was released before 2010.*

[source, cypher]
----
MATCH (m:Movie)
WHERE m.released < 2010
SET m:OlderMovie
RETURN DISTINCT labels(m)
----

[.thumb]
image::{guides}/img/AddOlderMovieLabel.png[AddOlderMovieLabel,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Retrieve all older movie nodes to test that the label was indeed added to these nodes.*

[source, cypher]
----
MATCH (m:OlderMovie)
RETURN m.title, m.released
----

[.thumb]
image::{guides}/img/RetrieveOlderMovies.png[RetrieveOlderMovies,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Add the label _Female_ to all _Person_ nodes that has a person whose name starts with _Robin_.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name STARTS WITH 'Robin'
SET p:Female
----

[.thumb]
image::{guides}/img/AddFemaleLabel.png[AddFemaleLabel,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Retrieve all _Female_ nodes.
Do you notice that not all of these nodes should be labeled _Female_?*

[source, cypher]
----
MATCH (p:Female)
RETURN p.name
----

[.thumb]
image::{guides}/img/RetrieveFemales.png[RetrieveFemales,width=400,float="right"]

The result returned should be:

Here we see that there are two _Person_ nodes.
The _Robin Williams_ node should not have the label _Female_.

++++
<div style="clear: both;"></div>
++++

*5. We've decided to not use the _Female_ label.
Remove the _Female_ label from the nodes that have this label.*

[source, cypher]
----
MATCH (p:Female)
REMOVE p:Female
----

[.thumb]
image::{guides}/img/RemoveFemales.png[RemoveFemales,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*6. View the current schema of the graph.*

[source, cypher]
----
CALL db.schema
----

[.thumb]
image::{guides}/img/SchemaAfterLabelAddition.png[SchemaAfterLabelAddition,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 3

== Instructions, Exercise 8, Part 3
.*Adding properties to nodes*

Next, you will add properties to specific nodes in the graph.

Write Cypher statements to:

. Add the following properties to the movie, _Forrest Gump_:
* released:  1994
* tagline: Life is like a box of chocolates...you never know what you're gonna get.
* lengthInMinutes: 142

*Hint:* This movie should also have the label _OlderMovie_.

.*Taking it further (optional)*
** Try adding or updating properties using the JSON-style syntax using `=` and `+=`.

[start=2]
. Retrieve this _OlderMovie_ node to confirm that the properties and label have been properly set.
. Add the following properties to the person, _Robin Wright_:

* born: 1966
* birthPlace: Dallas

[start=4]
. Retrieve this _Person_ node to confirm that the properties have been properly set.

.*Taking it further (optional)*
** Add properties to nodes using the JSON-style format where you add all of the properties to the node.
** Query the database to confirm your additions.
** Call the Cypher built-in method to retrieve all of the property keys in the graph.

== Solution, Exercise 8, Part 3
.*Adding properties to nodes*

Write Cypher statements to:

*1. Add the following properties to the movie, _Forrest Gump_:*

* *released:  1994*
* *tagline: Life is like a box of chocolates...you never know what you're gonna get.*
* *lengthInMinutes: 142*

*Hint:* This movie should also have the label _OlderMovie_.

[source, cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
SET m:OlderMovie, 
    m.released = 1994,
    m.tagline = "Life is like a box of chocolates...you never know what you're gonna get.",
    m.lengthInMinutes = 142
----

[.thumb]
image::{guides}/img/AddingPropertiesForrestGump.png[AddingPropertiesForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Retrieve this _OlderMovie_ node to confirm that the properties and label have been properly set.*

[source, cypher]
----
MATCH (m:OlderMovie)
WHERE m.title = 'Forrest Gump'
RETURN m
----

[.thumb]
image::{guides}/img/RetrieveForrestGump2.png[RetrieveForrestGump2,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Add the following properties to the person, _Robin Wright_:*

* *born: 1966*
* *birthPlace: Dallas*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name = 'Robin Wright'
SET p.born = 1966, p.birthPlace = 'Dallas'
----

[.thumb]
image::{guides}/img/SetRobinWrightProperties.png[SetRobinWrightProperties,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Retrieve this _Person_ node to confirm that the properties have been properly set.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p
----

[.thumb]
image::{guides}/img/RetrieveRobinWrightWithProperties.png[RetrieveRobinWrightWithProperties,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 4

== Instructions, Exercise 8, Part 4
.*Removing properties from nodes*

Next, you will remove properties from specific nodes in the graph.

Write Cypher statements to:

. Remove the _lengthInMinutes_ property from the movie, _Forrest Gump_.
. Retrieve the _Forrest Gump_ node to confirm that the property has been removed.
. Remove the _birthPlace_ property from the person, _Robin Wright_.
. Retrieve the _Robin Wright_ node to confirm that the property has been removed.

== Solution, Exercise 8, Part 4
.*Removing properties from nodes*

Write Cypher statements to:

*1. Remove the _lengthInMinutes_ property from the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
SET m.lengthInMinutes = null
----

[.thumb]
image::{guides}/img/RemoveLengthInMinutes.png[RemoveLengthInMinutes,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Retrieve the _Forrest Gump_ node to confirm that the property has been removed.*

[source, cypher]
----
MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN m
----

[.thumb]
image::{guides}/img/DeletedLengthInMinutesQuery.png[DeletedLengthInMinutesQuery,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Remove the _birthPlace_ property from the person, _Robin Wright_.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name = 'Robin Wright'
REMOVE p.birthPlace
----

[.thumb]
image::{guides}/img/RemovedBirthPlace.png[RemovedBirthPlace,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Retrieve the _Robin Wright_ node to confirm that the property has been removed.*

[source, cypher]
----
MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p
----

[.thumb]
image::{guides}/img/UpdatedRobinWright.png[UpdatedRobinWright,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise Summary

In this exercise, you wrote Cypher statements to create nodes.
Then you wrote Cypher statements to add and remove labels for nodes.
Next, you used Cypher to add properties to nodes.
Finally, you wrote Cypher statements to remove properties from nodes.

pass:a[<a play-topic='{guides}/09.html'>Continue to Exercise 9</a>]
