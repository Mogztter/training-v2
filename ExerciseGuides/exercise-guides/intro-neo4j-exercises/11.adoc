= Exercise 11
== Exercise 11: Merging Data in the Graph

=== Preparations

Make sure you have the Movie database, which contains 173 nodes and 258 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/AfterExercise10.png[After exercise 10,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/AfterExercise10.cypher[]
----


== Exercise Overview

You have gained experience creating, modifying, and deleting nodes and relationships.

In this exercise you will add and modify nodes and relationships in the graph by merging data.
In some parts of the exercise, you will be creating duplicate nodes and relationships.
You will see how in your real application, you should take care not to create duplicate nodes and relationships in your graphs.

. In Part 1, you merge data to create and update nodes.
. In Part 2, you merge data to create relationships.

Go to the next page to perform the two parts of this exercise.

== Instructions, Exercise 11, Part 1
.*Merging data to create and update nodes*

In this Part of the exercise, you will again create the movie, _Forrest Gump_, update the node, and then merge the data in the graph to create another node with a different label.

Write Cypher statements to:

. Use `MERGE` to create (`ON CREATE`) a node of type _Movie_ with the _title_ property, _Forrest Gump_.
If created,  set the _released_ property to  _1994_.
. Use `MERGE` to update (`ON MATCH`) a node of type _Movie_ with the _title_ property, _Forrest Gump_.
If found, set the tagline property to 
"Life is like a box of chocolates...you never know what you're gonna get.".
. Use `MERGE` to create (`ON CREATE`) a node of type _Production_ with the _title_ property, _Forrest Gump_.
If created, set the property _year_ to the value _1994_.
. Query the graph to find all labels for nodes with the title property, _Forrest Gump_.
. Use `MERGE` to update (`ON MATCH`) the existing _Production_ node for _Forrest Gump_ to add the _company_ property with a value of _Paramount Pictures_.
. Use `MERGE` to add the _OlderMovie_ label to the movie, _Forrest Gump_..

== Solution, Exercise 11, Part 1
.*Merging data to create and update nodes*

In this Part of the exercise, you will again create the movie, _Forrest Gump_, update the node, and then merge the data in the graph to create another node with a different label.

Write Cypher statements to:

*1. Use `MERGE` to create (`ON CREATE`) a node of type _Movie_ with the _title_ property, _Forrest Gump_.
If created,  set the _released_ property to  _1994_.*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON CREATE SET m.released = 1994
RETURN m
----

[.thumb]
image::{guides}/img/CreateForrestGump2.png[CreateForrestGump2,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Use `MERGE` to update (`ON MATCH`) a node of type _Movie_ with the _title_ property, _Forrest Gump_.
If found, set the tagline property to 
"Life is like a box of chocolates...you never know what you're gonna get.".*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON CREATE SET m.released = 1994
ON MATCH SET m.tagline = "Life is like a box of chocolates...you never know what you're gonna get."
RETURN m
----

[.thumb]
image::{guides}/img/MergeForrestGump.png[MergeForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3.  Use `MERGE` to create (`ON CREATE`) a node of type _Production_ with the _title_ property, _Forrest Gump_.
If created, set the property _year_ to the value _1994_.*

[source, cypher]
----
MERGE (p:Production {title: 'Forrest Gump'})
ON CREATE SET p.year = 1994
RETURN p
----

[.thumb]
image::{guides}/img/MergeProduction.png[MergeProduction,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Query the graph to find labels for nodes with the title property, _Forrest Gump_.*

[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump'
RETURN  labels(m)
----

[.thumb]
image::{guides}/img/AllForrestGumps.png[AllForrestGumps,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*5. Use `MERGE` to update (`ON MATCH`) the existing _Production_ node for _Forrest Gump_ to add the _company_ property with a value of _Paramount Pictures_.*

[source, cypher]
----
MERGE (p:Production {title: 'Forrest Gump'})
ON MATCH SET p.company = 'Paramount Pictures'
RETURN p
----

[.thumb]
image::{guides}/img/AddCompany.png[AddCompany,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*6. Use `MERGE` to add the _OlderMovie_ label to the movie, _Forrest Gump_.*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON MATCH SET m:OlderMovie
RETURN labels(m)
----

[.thumb]
image::{guides}/img/AddOlderMovieLabelToForrestGump.png[AddOlderMovieLabelToForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 2

== Instructions, Exercise 11, Part 2
.*Merging data to create relationships*

In this Part of the exercise, you merge data to create relationships.

. Execute the following Cypher statement:

[source, cypher]
----
MERGE (p:Person {name: 'Robert Zemeckis'})-[:DIRECTED]->(m {title: 'Forrest Gump'})
----

This statement first finds all _Person_ nodes that have only the _name_ property value of _Robert Zemeckis_.
It then finds all nodes with only the _title_ property set to _Forrest Gump_.
There are no _Person_ or other nodes that have only these properties so the graph engine creates them.
Then the graph engine creates the relationship between these two nodes.
That is, this `MERGE` operation creates two nodes and a single relationship.
If we had provided all of the property values for the nodes, we would not have created the extra nodes.

In fact, you should *never* create nodes and relationships together like this! This example is here to show you how powerful Cypher can be.
A best practice is to create nodes first, then relationships.

[start=2]
. Repeat the execution of this statement.
It should do nothing.
A best practice is to *only* use `MERGE` to create relationships to ensure that there will be no duplication in the graph when you are unsure if the relationship exists.
. Delete the duplicated nodes ensuring that the relationships are also deleted.
*Hint:* Query the nodes before you delete them to ensure you have the correct `MATCH` clauses.
[start=4]
. Use `MERGE` to create the _DIRECTED_ relationship between _Robert Zemeckis_ and  the _Movie_, _Forrest Gump_.
. Use `MERGE` to create the _ACTED_IN_ relationship between the actors, _Tom Hanks_, _Gary Sinise_, and _Robin Wright_ and the _Movie_, Forrest Gump_.
. Modify the relationship property, _role_ for their roles in _Forrest Gump_:
* _Tom Hanks_ is _Forrest Gump_
* _Gary Sinise_ is _Lt. Dan Taylor_
* _Robin Wright_ is _Jenny Curran_

== Solution, Exercise 11, Part 2
.*Merging data to create relationships*

In this Part of the exercise, you merge data to create relationships.

*1. Execute the following Cypher statement:*

[source, cypher]
----
MERGE (p:Person {name: 'Robert Zemeckis'})-[:DIRECTED]->(m {title: 'Forrest Gump'})
----

This statement first finds all _Person_ nodes that have [.underline[#only# the _name_ property value of _Robert Zemeckis_.
It then finds all nodes with [.underline[#only# the _title_ property set to _Forrest Gump_.
There are no _Person_ or other nodes that have only these properties so the graph engine creates them.
Then the graph engine creates the relationship between these two nodes.
That is, this `MERGE` operation creates two nodes and a single relationship.
If we had provided all of the property values for the nodes, we would not have created the extra nodes.

In fact, you should *never* create nodes and relationships together like this! This example is here to show you how powerful Cypher can be.
A best practice is to create nodes first, then relationships.

[.thumb]
image::{guides}/img/MergeNodesAndRelationships.png[MergeNodesAndRelationships,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Repeat the execution of this statement.
It should do nothing.
A best practice is to always use `MERGE` to create relationships to ensure that there will be no duplication in the graph.* 

[source, cypher]
----
MERGE (p:Person {name: 'Robert Zemeckis'})-[:DIRECTED]->(m {title: 'Forrest Gump'})
----

[.thumb]
image::{guides}/img/MergeNodesAndRelationships2.png[MergeNodesAndRelationships2,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Delete the duplicated nodes ensuring that the relationships are also deleted.*

*Hint:* Query the nodes before you delete them to ensure you have the correct `MATCH` clauses.

Find the correct _Person_ node first:
[source, cypher]
----
MATCH (p:Person {name: 'Robert Zemeckis'})-[rel]-(x)
WHERE NOT EXISTS (p.born)
RETURN p, rel, x
----

[.thumb]
image::{guides}/img/FindPersonNodeBeforeDelete.png[FindPersonNodeBeforeDelete,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Delete this _Person_ node, along with its relationships:
[source, cypher]
----
MATCH (p:Person {name: 'Robert Zemeckis'})--()
WHERE NOT EXISTS (p.born)
DETACH DELETE p
----

[.thumb]
image::{guides}/img/DeletedExtraPerson.png[DeletedExtraPerson,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Find the correct _Forrest Gump_ node to delete:
[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump' AND labels(m) = []
RETURN m, labels(m)
----

[.thumb]
image::{guides}/img/ForrestGumpToDelete.png[ForrestGumpToDelete,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Delete this _Forrest Gump_ node.
It should have no relationships, but you can specify DETACH just to be certain:
[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump' AND labels(m) = []
DETACH DELETE m
----

[.thumb]
image::{guides}/img/DeletedExtraForrestGump.png[DeletedExtraForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Use `MERGE` to create the _DIRECTED_ relationship between _Robert Zemeckis_ and  the _Movie_, _Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person), (m:Movie)
WHERE p.name = 'Robert Zemeckis' AND m.title = 'Forrest Gump'
MERGE (p)-[:DIRECTED]->(m)
----

[.thumb]
image::{guides}/img/AddDirectedToForrestGump.png[AddDirectedToForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*5. Use `MERGE` to create the _ACTED_IN_ relationship between the actors, _Tom Hanks_, _Gary Sinise_, and _Robin Wright_ and the _Movie_, Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person), (m:Movie)
WHERE p.name IN ['Tom Hanks','Gary Sinise', 'Robin Wright']
      AND m.title = 'Forrest Gump'
MERGE (p)-[:ACTED_IN]->(m)
----

[.thumb]
image::{guides}/img/ActedInForrestGump.png[ActedInForrestGump,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++


*6. Modify the relationship property, _role_ for their roles in _Forrest Gump_:*
* _Tom Hanks_ is _Forrest Gump_
* _Gary Sinise_ is _Lt. Dan Taylor_
* _Robin Wright_ is _Jenny Curran_

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
SET rel.roles = 
CASE p.name
  WHEN 'Tom Hanks' THEN ['Forrest Gump']
  WHEN 'Robin Wright' THEN ['Jenny Curran']
  WHEN 'Gary Sinise' THEN ['Lt. Dan Taylor']
END
----

[.thumb]
image::{guides}/img/SetActedRoles.png[SetActedRoles,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise Summary

In this exercise you added and modified nodes and relationships in the graph by merging data.
In some parts of the exercise, you created duplicate nodes and relationships.
You should take care not to create duplicate nodes and relationships in your real application graphs.

pass:a[<a play-topic='{guides}/12.html'>Continue to Exercise 12</a>]
