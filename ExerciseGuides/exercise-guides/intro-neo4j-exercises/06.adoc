= Exercise 6
== Exercise 6: Controlling results returned

=== Preparations

Make sure you have the Movie database, which contains 171 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/InitialDatabase.png[InitialDatabase,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/initialDatabase.cypher[]
----

== Exercise Overview

In the previous exercise, you wrote queries to perform additional filtering, count and collect results, as well as to perform some intermediate processing during the query.

In this exercise, you will write queries where you can customize what results are returned and if they will be sorted.
First, you will write a query that eliminates duplication of rows returned.
Then you write a query that sorts the data returned.
Finally, you will write a query to limit the number of results returned.

. In Part 1, you use the `DISTINCT` keyword to eliminate duplication.
. In Part 2, you use the `ORDER BY` keyword to sort results returned.
. In Part 3, you write queries to limit the number of results returned.

Go to the next page to perform the three parts of this exercise.

== Instructions, Exercise 6, Part 1
.*Eliminating duplication*

Write the following Cypher queries:

. You want to know what actors acted in movies in the decade starting with the year 1990. First write a query to retrieve all actors that acted in movies during the 1990s, where you return the released date, the movie title, and the collected actor names for the movie.
For now do not worry about duplication.
. The results returned from the previous query include multiple rows for a movie released value.
Next, modify the query so that the released date records returned are not duplicated.
To implement this, you must add the collection of the movie titles to the results returned.
. The results returned from the previous query returns the collection of movie titles with duplicates.
That is because there are multiple actors per released year.
Next, modify the query so that there is no duplication of the movies listed for a year.

== Solution, Exercise 6, Part 1
.*Eliminating duplication*

Write the following Cypher queries:

*1. You want to know what actors acted in movies in the decade starting with the year 1990.
First write a query to retrieve all actors that acted in movies during the 1990s, where you return the released date, the movie title, and the collected actor names for the movie.
For now do not worry about duplication.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN DISTINCT m.released, m.title, collect(a.name)
----

[.thumb]
image::{guides}/img/NinetiesMovies1.png[NinetiesMovies1,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. The results returned from the previous query include multiple rows for a movie released value.
Next, modify the query so that the released date records returned are not duplicated.
To implement this, you must add the collection of the movie titles to the results returned.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(m.title), collect(a.name)
----

[.thumb]
image::{guides}/img/NinetiesMovies2.png[NinetiesMovies2,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. The results returned from the previous query returns the collection of movie titles with duplicates.
That is because there are multiple actors per released year.
Next, modify the query so that there is no duplication of the movies listed for a year.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(DISTINCT m.title), collect(a.name)
----

[.thumb]
image::{guides}/img/NinetiesMovies3.png[NinetiesMovies3,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 2

== Instructions, Exercise 6, Part 2
.*Ordering results.*

Modify the query that you just wrote to order the results returned so that the more recent years are displayed first.

== Solution, Exercise 6, Part 2
.*Ordering results.*

*Modify the query that you just wrote to order the results returned so that the more recent years are displayed first.*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(DISTINCT m.title), collect(a.name)
ORDER BY m.released DESC
----

[.thumb]
image::{guides}/img/OrderBy.png[OrderBy,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Next, Part 3

== Instructions, Exercise 6, Part 3
.*Limiting results returned*

Write the following Cypher queries:

. Retrieve the top 5 ratings and their associated movies, returning the movie title and the rating.
. Retrieve all actors that have not appeared in more than 3 movies.
Return their names and list of movies.

== Solution, Exercise 6, Part 3
.*Limiting results returned*

Write the following Cypher queries:

*1. Retrieve the top 5 ratings and their associated movies, returning the movie title and the rating.*

[source, cypher]
----
MATCH (:Person)-[r:REVIEWED]->(m:Movie)
RETURN  m.title AS movie, r.rating AS rating
ORDER BY r.rating DESC LIMIT 5
----

[.thumb]
image::{guides}/img/Limit1.png[Limit1,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Retrieve all actors that have not appeared in more than 3 movies.
Return their names and list of movies*

[source, cypher]
----
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WITH  a,  count(a) AS numMovies, collect(m.title) AS movies
WHERE numMovies <= 3
RETURN a.name, movies
----

[.thumb]
image::{guides}/img/AtMost3MoviesPerActor.png[AtMost3MoviesPerActor,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise Summary

In this exercise, you wrote queries where you can customize what results are returned and if they will be sorted.
First, you wrote a query that eliminates duplication of rows returned.
Then you wrote a query that sorts the data returned.
Finally, you wrote a query to limit the number of results returned.

pass:a[<a play-topic='{guides}/07.html'>Continue to Exercise 7</a>]
