= Exercise 9
== Exercise 9: Creating Relationships

=== Preparations

Make sure you have the Movie database, which contains 173 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::{guides}/img/AfterExercise8.png[After exercise 8,200,200, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/AfterExercise8.cypher[]
----



== Exercise Overview

In the previous exercise, you created nodes, added and removed both properties and labels from nodes.
The connections between nodes, the relationships, are one of the most important parts of a Neo4j graph.

In this exercise you will create relationships between nodes.
Then you will add properties to relationships.
Finally, you will remove properties from relationships.

. In Part 1, you create relationships.
. In Part 2, you add properties to relationships.
. In Part 3, you remove properties from relationships.

Go to the next page to perform the three parts of this exercise.

== Instructions, Exercise 9, Part 1
.*Creating relationships*

In the last exercise, you created the node for the movie, _Forrest Gump_ and the person, _Robin Wright_.

Write Cypher statements to:

. Create the _ACTED_IN_ relationship between the actors, _Robin Wright_, _Tom Hanks_, and _Gary Sinise_ and the movie, _Forrest Gump_.
. Create the _DIRECTED_ relationship between _Robert Zemeckis_ and the movie, _Forrest Gump_.
. Create a new relationship, _HELPED_ from _Tom Hanks_ to _Gary Sinise_.
. Write a Cypher query to return all nodes connected to the movie, _Forrest Gump_, along with their relationships.

== Solution, Exercise 9, Part 1
.*Creating relationships*

Write Cypher statements to:

*1. Create the _ACTED_IN_ relationship between the actors, _Robin Wright_, _Tom Hanks_, and _Gary Sinise_ and the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (m:Movie) 
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Tom Hanks' OR p.name = 'Robin Wright' OR p.name = 'Gary Sinise'
CREATE (p)-[:ACTED_IN]->(m)
----

[.thumb]
image::{guides}/img/CreateThreeRelationships.png[CreateThreeRelationships,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Create the _DIRECTED_ relationship between _Robert Zemeckis_ and the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (m:Movie) 
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Robert Zemeckis'
CREATE (p)-[:DIRECTED]->(m)
----

[.thumb]
image::{guides}/img/AddDirectedRelationship.png[AddDirectedRelationship,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Create a new relationship, _HELPED_ from _Tom Hanks_ to _Gary Sinise_.*

[source, cypher]
----
MATCH (p1:Person) 
WHERE p1.name = 'Tom Hanks'
MATCH (p2:Person)
WHERE p2.name = 'Gary Sinise'
CREATE (p1)-[:HELPED]->(p2)
----

[.thumb]
image::{guides}/img/CreateHELPEDRelationship.png[CreateHELPEDRelationship,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. Write a Cypher query to return all nodes connected to the movie, _Forrest Gump_, along with their relationships.*

[source, cypher]
----
MATCH (p:Person)-[rel]-(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p, rel, m
----

[.thumb]
image::{guides}/img/ForrestGumpRelationships.png[ForrestGumpRelationships,width=400,float="right"]

The result returned should be:

Notice here that the _HELPED_ relationship is also shown because _Tom Hanks_ and _Gary Sinise_ are connected and we have _Connect result nodes_ selected in our Neo4j Browser settings.

++++
<div style="clear: both;"></div>
++++

Next, Part 2

== Instructions, Exercise 9, Part 2
.*Adding properties to relationships*

Next, you will add some properties to the relationships that you just created.

Create Cypher statements to do the following:

. Add the _roles_ property to the three _ACTED_IN_ relationships that you just created to the movie, _Forrest Gump_ using this information:
* _Tom Hanks_ played the role, _Forrest Gump_.
* _Robin Wright_ played the role, _Jenny Curran_.
* _Gary Sinise_ played the role, _Lieutenant Dan Taylor_.

*Hint:* You can set each relationship using separate `MATCH` clauses.
You can also use a `CASE` clause to set the values.
Look up in the documentation for how to use the `CASE` clause.

[start=2]
. Add a new property, _research_ to the _HELPED_ relationship between _Tom Hanks_ and _Gary Sinise_ and set this property's value to _history_.
. View the current list of property keys in the graph.
. View the current schema of the graph.
. Query the graph to return the names and roles of actors in the movie, _Forrest Gump_.
. Query the graph to retrieve information about any _HELPED_ relationships.

.*Taking it further (optional)*
** Try adding or updating properties using the JSON-style syntax using `=` and `+=`.

== Solution, Exercise 9, Part 2
.*Adding properties to relationships*

Create Cypher statements to do the following:

*1. Add the _roles_ property to the three _ACTED_IN_ relationships that you just created to the movie, _Forrest Gump_ using this information:*
* *_Tom Hanks_ played the role, _Forrest Gump_.*
* *_Robin Wright_ played the role, _Jenny Curran_.*
* *_Gary Sinise_ played the role, _Lieutenant Dan Taylor_.*

*Hint:* You can set each relationship using separate `MATCH` clauses.
You can also use a `CASE` clause to set the values.
Look up in the documentation for how to use the `CASE` clause.

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
SET rel.roles = 
CASE p.name
  WHEN 'Tom Hanks' THEN ['Forrest Gump']
  WHEN 'Robin Wright' THEN ['Jenny Curran']
  WHEN 'Gary Sinise' THEN ['Lieutenant Dan Taylor']
END
----

[.thumb]
image::{guides}/img/SetPropertiesUsingCase.png[SetPropertiesUsingCase,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Add a new property, _research_ to the _HELPED_ relationship between _Tom Hanks_ and _Gary Sinise_ and set this property's value to _war history_.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
SET rel.research = 'war history'
----

[.thumb]
image::{guides}/img/SetResearchProperty.png[SetResearchProperty,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. View the current list of property keys in the graph.*

[source, cypher]
----
call db.propertyKeys
----

[.thumb]
image::{guides}/img/PropertyKeys-research.png[PropertyKeys-research,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*4. View the current schema of the graph.*

[source, cypher]
----
call db.schema
----

[.thumb]
image::{guides}/img/SchemaAfterAddingRelatiobnships.png[SchemaAfterAddingRelatiobnships,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++
*5. Query the graph to return the names and roles of actors in the movie, _Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p.name, rel.roles
----

[.thumb]
image::{guides}/img/ForrestGumpRoles.png[ForrestGumpRoles,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*6. Query the graph to retrieve information about any _HELPED_ relationships.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]-(p2:Person)
RETURN p1.name, rel, p2.name
----

[.thumb]
image::{guides}/img/HELPEDProperties.png[HELPEDProperties,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

Notice here that 2 results are returned.
That is because we did not specify a direction for the relationship in the query.
So there is a relationship from _Tom Hanks_ and also to _Gary Sinise_, even though there is only one physical relationship in the graph.
If you specify a direction for the relationship, only one result is returned.

Next, Part 3

== Instructions, Exercise 9, Part 3
.*Updating and removing properties from a relationship*

Next, you will modify existing properties for a relationship and also remove them.

Write Cypher statements to:

. Modify the _role_ that _Gary Sinise_ played in the movie, _Forrest Gump_ from _Lieutenant Dan Taylor_ to _Lt. Dan Taylor_.
. Remove the _research_ property from the _HELPED_ relationship from _Tom Hanks_ to _Gary Sinise_.
. Query the graph to confirm that your modifications were made to the graph.

== Solution, Exercise 9, Part 3
.*Updating and removing properties from a relationship*

Next, you will modify existing properties for a relationship and also remove them.

Write Cypher statements to:

*1. Modify the _role_ that _Gary Sinise_ played in the movie, _Forrest Gump_ from _Lieutenant Dan Taylor_ to _Lt. Dan Taylor_.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump' AND p.name = 'Gary Sinise'
SET rel.roles =['Lt. Dan Taylor']
----

[.thumb]
image::{guides}/img/UpdateGarySiniseRole.png[UpdateGarySiniseRole,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*2. Remove the _research_ property from the _HELPED_ relationship from _Tom Hanks_ to _Gary Sinise_.*

[source, cypher]
----
MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
REMOVE rel.research
----

[.thumb]
image::{guides}/img/RemoveResearchProperty.png[RemoveResearchProperty,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

*3. Query the graph to confirm that your modifications were made to the graph.*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
return p, rel, m
----

[.thumb]
image::{guides}/img/GarySiniseRoleUpdate.png[GarySiniseRoleUpdate,width=400,float="right"]

The result returned should be:

++++
<div style="clear: both;"></div>
++++

== Exercise Summary

In this exercise you created relationships between nodes.
Then you added properties to relationships.
Finally, you removed properties from relationships.

pass:a[<a play-topic='{guides}/10.html'>Continue to Exercise 10</a>]
