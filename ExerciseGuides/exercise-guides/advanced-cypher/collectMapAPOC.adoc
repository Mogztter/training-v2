
== Data Structure Manipulation

_(with some APOC's thrown in)_

image:http://assets.neo4j.org/img/neo4j_logo.png[] Version 1.0 - Dan F.

These data structures are useful for manipulating Cypher results as well as
returning data to clients that ingest json like structures. Cypher itself has
literal maps.  e.g. You can do this:

//setup
//output
[source,cypher,subs=attributes]
CREATE (x:NomNom { count:[1,2,3], nom1: 1, nom2:2 } ) return x

Neo4j does not store nested maps, so the data structure can be flattened to
store as a node. You'll get an error like this if you try and nest:

*Neo.ClientError.Statement.TypeError*
....
Neo.ClientError.Statement.TypeError: Property values can only be of primitive types or arrays thereof
....
The CREATE below will give the `only of primitive types` error:
//setup
//output
[source,cypher,subs=attributes]
CREATE (x:NomNom {  nomnom:{nom:'nom', nom:'nom'}, NomNoms:[{nom:'nom'}, {nom:'nom'}] } ) return x

== APOC's for Lists and Maps

`.map` and `.coll` apoc's are available to manipulate data structures because Cypher can't change keys, values, merge maps, etc.
//setup
//output
[source,cypher,subs=attributes]
CALL apoc.help(".map")

== Create A Parameter With Some Data
Here's an example of maps, lists, and nested lists / maps used as a parameter.


//setup
//output
[source,cypher,subs=attributes]
:param data => {
  name: "Michael",
  age: 44,
  kids:{
    Augie: 21,
    Miska: 18,
    Sue: 16
  },
  pets:[
    {name: "Dan", specie: "dog", age: 7},
    {name: "Annie", specie: "cat"}
  ]
}

The arrow syntax allows for arbitrary Cypher code.

//setup
//output
[source,cypher,subs=attributes]
RETURN $data

The data structure stored in the $data variable is nested.
....
$data
{
  {
  "name": "Michael",
  "pets": [
    {
      "name": "Dan",
      "specie": "dog",
      "age": 7
    },
    {
      "name": "Annie",
      "specie": "cat"
    }
  ],
  "age": 44,
  "kids": {
    "Augie": 21,
    "Sue": 16,
    "Miska": 18
  }
}
....

== Using data structures
But let's not concern ourselves with pets. apoc.map.removeKey will delete pets
maps.
//setup
//output
[source,cypher,subs=attributes]
RETURN  apoc.map.removeKey($data, "pets") as myData

Let's give the node Michael some kids
//setup
//output
[source,cypher,subs=attributes]
WITH  apoc.map.removeKey($data, "pets") as myData
MERGE (p:Person {name: myData.name})
SET p += myData.kids
RETURN p

NOTE: Use `+=` to update existing values and / or append new values. Using `=`
will delete all existing data and replace it with new data (e.g. you can lose
values)

You can flatten one level of maps to get name that matches the path:
//setup
//output
[source,cypher,subs=attributes]
RETURN apoc.map.flatten(apoc.map.removeKey($data, "pets"))

We did this without pets by using `apoc.map.removeKey()`.  Try the same statement
without `apoc.map.removeKey()`

== FOREACH / UNWIND With Maps

. Let's temporarily change our `$data.name` value to be `'Rocky'` using
`apoc.map.setKey()`
. ... and add the pets into the graph

//setup
//output
[source,cypher,subs=attributes]
WITH apoc.map.setKey($data, 'name',  'Rocky') AS myRockyName
FOREACH (p IN $data.pets | MERGE(per:Person {name: 'Rocky'})-[:HAS_PET]->(pet:Pet {name: p.name})
ON CREATE SET pet = p {.*, trained: FALSE}
ON MATCH SET pet += p {.*, trained: TRUE} )

NOTE: Look at the use of the map shorthand `p {.*}`.  This is referred to as a
_map projection_.  Here we use map projections combined with explicit map
variables to create all the attributes maps in `$data.pets`, plus the addition
of a new map `.trained = FALSE`.  Using a map projection is a heck of a lot easier
than typing out pet.name = p.name, pet.specie = p.specie,  ...especially  if
the attributes are not consistent or definitive on every node.

*_But..._* That wasn't what we wanted!  Look at what the FOREACH created:
//setup
//output
[source,cypher,subs=attributes]
MATCH path=(p:Person {name: 'Rocky'})-->(a) RETURN path


MERGE (and CREATE) work on the whole path _pattern_. For example new nodes
and relationships will be created if the
`(:Pet {name: p.name})` already exists, but the `(per:Person {name: $data.name})-[:HAS_PET]->(:Pet {name: p.name}) )`
does not.

DELETE the bogus data, and anything attached to it _(alwaseys be careful with this)_:
//setup
//output
[source,cypher,subs=attributes]
MATCH path=(p:Person {name: 'Rocky'})-->(a) DETACH DELETE path


A simple solution to avoiding creating multiple nodes in the FOREACH above is
by binding variables, with using UNWIND as the simplest solution.

In the Cypher below:

. `apoc.map.setKey()` is used to replace the $data.name value of 'Michael' with
'Rocky'.
. Look at the use of bound variables, such as per, myRockyName, etc. to
anchor the nodes being updated.

//setup
//output
[source,cypher,subs=attributes]
WITH apoc.map.setKey($data, 'name',  'Rocky') AS myRockyName
MERGE (per:Person {name: myRockyName.name})
WITH myRockyName.pets AS pets, per AS myRocky
UNWIND pets AS pet
MERGE (myPet:Pet {name: pet.name})
SET myPet = pet {.*, trained: 'no'}
MERGE path=(myRocky)-[:HAS_PET]->(myPet)
RETURN path


NOTE: The `SET myPet = pet {.*, trained: 'no'}` will clear out all previous
values.  Use `+=` to update existing values and / or append new values.  The
Cypher below will create or update the properties on the :Pet nodes to
have `adopted: TRUE, trained: 'yes'`

//setup
//output
[source,cypher,subs=attributes]
MERGE (pet:Pet) ON MATCH SET pet += {adopted: TRUE, trained: 'yes'} RETURN pet

== An Aside on Using FOREACH and UNWIND Together

Here's an example from a question posted on the Neo4j Community site. The goals
is to connect the 'Environment_elements' and 'System_element' based on the
'Connections' data. For this example the data is stored in a parameter
named 'value':


//setup
//output
[source,cypher,subs=attributes]
:param value => {
    Environment: {
        Environment_elements: [{
                name: "weather",
                id: 1
            },
            {
                name: "location",
                id: 2
            },
            {
                name: "operator",
                id: 3
            }
        ],
        System_element: {
            name: "system",
            id: 4
            },
        Connections: [{
                name: "weather_conditions",
                id: 4,
                source: 1,
                target: 4
            },
            {
                name: "location_connection",
                id: 5,
                source: 2,
                target: 4
            },
            {
                name: "operator_connection",
                id: 6,
                source: 3,
                target: 4
            }
        ]
    }
}

The 'FOREACH' creates each ':Element' node.  The 'UNWIND' connects all the nodes
by using multiple sequential 'MERGE' statements which you cannot do in a single
'FOREACH'. Notice the concatenation of the 'Environment_elements + System_element'
data into one list in the 'FOREACH'.


//setup
//output
[source,cypher,subs=attributes]
FOREACH (node in $value.Environment.Environment_elements +  [$value.Environment.System_element] |
  MERGE (e:Element {id: node.id})
  SET e.name = node.name
)
WITH $value AS value
UNWIND value.Environment.Connections AS connection
MERGE (e1:Element {id: connection.source})
MERGE (e2:Element {id: connection.target})
MERGE (e1)-[:CONNECTED]->(e2)


_Copy and paste the link into a browser window if you want to see the post:_
`https://community.neo4j.com/t/creating-nodes-from-nested-json-using-apoc-load-json-procedure/2618`



== APOC merge maps and groupBy's
Let's have all the movies created in 1975 be pirated (illegal copies made) by:

. Using to `apoc.map.merge()` to merge the two map collections that will be our
new pirated node properties.
. The two maps consist of:
.. The first map is all the node properties that we want or need to change for our
new node that is created by `apoc.map.removeKeys`. Remember that the :Movie(id)
property has a `UNIQUE CONSTRAINT` on it.
.. The `toUpper()` function capitalizes the title to make it easier to identify
the pirated versions.  The unique :Movie(id) will be the original id * -1.

//setup
//output
[source,cypher,subs=attributes]
MATCH path=(origMovies:Movie {releaseYear: 1975}) WHERE origMovies.pirate IS NULL
UNWIND nodes(path) AS origMovie
MERGE (bootLeg:Movie {title: toUpper(origMovie.title), id: toInteger(origMovie.id) * -1, pirate: TRUE})
ON CREATE SET bootLeg +=  apoc.map.removeKeys(origMovie, ['title', 'id'])
RETURN bootLeg

Now use `apoc.map.groupBy()` to return the _a single_ movie released in 1975:

//setup
//output
[source,cypher,subs=attributes]
MATCH (m:Movie) WHERE m.releaseYear = 1975
WITH collect(properties(m)) AS movies
RETURN apoc.map.groupBy(movies, 'releaseYear')


Use `apoc.map.groupByMulti()` to return all the movies released in 1975

//setup
//output
[source,cypher,subs=attributes]
MATCH (m:Movie) WHERE m.releaseYear = 1975
WITH collect(properties(m)) AS movies
RETURN apoc.map.groupByMulti(movies, 'releaseYear')

== Summary of Pattern Comprehension / Map Projections
Map projections and pattern comprehension are very powerful that can do things
such as including data based on a Cypher pattern.

Map projections take a map or entity (node or relationship) and apply a map-like
property-selector to it.

Within a map projection you can also add literal values or aggregations to the
data that you extract from the entity.

[source,cypher]
----
entity { .property1, .property2, .*,  literal: value,  values: collect(numbers), variable}
----
Here is a full list of possible selectors:

[options=header,cols="1m,2a"]
|===
| syntax | description
| `.property` | property lookup
|`.*` | all properties
|`variable` | variable name as key, variable value as value
|`key : value` | literal entry
|===

Here's an example of a map projection:

//setup
//output
[source,cypher,subs=attributes]
MATCH (actor:Person { name: 'Charlie Sheen' })-[:ACTED_IN]->(movie:Movie)
RETURN actor { .name, .propDoesNotExist, movies: collect(movie { .title, .releaseYear })}


Pattern comprehension is a syntactic construct available in Cypher for creating
a list based on matchings of a pattern. A pattern comprehension will
match the specified pattern just like a normal MATCH clause, with predicates
just like a normal WHERE clause, but will yield a custom projection as specified.

To return all the :Actors
that :ACTED_IN 'The Matrix':

//setup
//output
[source,cypher,subs=attributes]
MATCH (m:Movie) WHERE m.title = 'The Matrix'
RETURN
m {.title, .releaseYear,
   directors: [ (m)<-[:DIRECTED]-(a) | a {.name, .born } ],
   actors: [ (m)<-[:ACTED_IN]-(a) | a {.name, .born,
       movies:[(a)-[:ACTED_IN]->(m2) | m2 { .title, .releaseYear }]
  }]} as document
