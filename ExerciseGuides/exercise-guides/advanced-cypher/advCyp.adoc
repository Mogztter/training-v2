
== Advanced Loading Techniques

image:http://assets.neo4j.org/img/neo4j_logo.png[] Version 1.0 - Dan F.

This graphgist covers different scenarios for loading csv files and creating data.  These techniques often
apply to loading other data types and Cypher in general.

The exercise creates a Movie database that *_is not_* the same as the Movie database
created by running :play movies

== LOAD CSV - Data Inspection

Now that we’ve got an overview of the CSV files it’s time to take a closer look at them. We’re going to use the LOAD CSV command to do exploration.

The following are some issues we’ve seen with CSV files:

 - Incorrect header vs. data

 - Wrong quotes, unquoted newlines, stray quotes

 - UTF-8 prefix

 - Trailing spaces, typos, binary zeros

Even if everything looks fine using our command line tools it’s still good to see how Cypher sees those same files.

== Row Count

Start with the count of the number of lines in the input file.

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/new_movies.csv' AS row
RETURN count(*);

== Row as List Data
//setup
//output
[source,cypher,subs=attributes]
LOAD CSV FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/new_movies.csv' AS row
RETURN *
LIMIT 5

== LOAD CSV - WITH HEADERS

If we include WITH HEADERS along with the LOAD CSV command we’ll get back a *_map_* per row. Let’s try that out.

.Row as map data
//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/new_movies.csv' AS row
RETURN row, keys(row)
LIMIT 5

== Data Conversion
By default data is imported as strings but we can coerce the data using the toInteger and toFloat functions. The split function is useful for processing
//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/new_movies.csv' AS row
RETURN toFloat(row.avgVote) as avgVote, toInteger(row.releaseYear) as releaseYear, split(row.genres,":") as genres, row
LIMIT 5;

== Exercise: Creating Data (10')

Find your favorite actor and a movie they acted in from the CSV files and manually CREATE the Movie and MERGE the :Person, e.g.
....
Label -> :Movie
Properties:
  id -> integer unique identifier
  title -> "Movie title" (e.g. "Forrest Gump")
  genres -> A list (e.g. ["Comedy","Drama","Romance"] )
....

_Look at the CSV structure for property-names and types._

*Solution on next page*

== Answer: Creating Data
//setup
//output
[source,cypher,subs=attributes]
// CREATE a :Movie
CREATE (:Movie {
          id:5574,
          title:"Forrest Gump",
          tagline: "Life was like a box of chocolates, you never know what you're gonna get",
          avgVote:7.7,
          releaseYear:1994,
          genres: ["Comedy","Drama","Romance"] });

//setup
//output
[source,cypher,subs=attributes]
MERGE (p:Person {id: 31})
ON CREATE SET p.name = "Tom Hanks", p.birthYear = 1956;

//setup
//output
[source,cypher,subs=attributes]
MATCH (p:Person {id:31}), (m:Movie {id:5574})
CREATE (p)-[:ACTED_IN {roles:['Forrest Gump']}]->(m);

//setup
//output
[source,cypher,subs=attributes]
MATCH path=(p:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(m:Movie) RETURN path

== LOAD CSV - Index / Constraint Preparation

For the next exercises you will need to create indexes & constraints especially for relationship creation.

*Note:* Indexes and constraints can be created on :Labels and .properties that never populated in the database.

//setup
//output
[source,cypher,subs=attributes]
CREATE CONSTRAINT ON (m:Movie)
ASSERT m.id IS UNIQUE

//setup
//output
[source,cypher,subs=attributes]
CREATE CONSTRAINT ON (p:Person)
ASSERT p.id IS UNIQUE

//setup
//output
[source,cypher,subs=attributes]
CREATE INDEX ON :Person(name)

//setup
//output
[source,cypher,subs=attributes]
CREATE INDEX ON :Movie(title)

Verify that the indexes are created:

CREATE INDEX ON :Person(name)
CREATE INDEX ON :Movie(title)

Look at the "provider" output column for the db.index output
//setup
//output
[source,cypher,subs=attributes]
call db.indexes()

Verify that the constraints are created:
CONSTRAINT ON (m:Movie)
CONSTRAINT ON (p:Person)
//setup
//output
[source,cypher,subs=attributes]
call db.constraints()

== LOAD CSV - Import Approaches

 - normalized data
 - denormalized data - multi-pass
 - denormalized data - single-pass
 - optimizations

_You will be importing_
 :Movies, :Person, :ACTED_IN

== Loading Normalized Data

Approach

 - Separate CSV files
 - Create nodes individually, one per label
 - Create relationships, one per type


== Import Movies _(Normalized Data)_

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM
     'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/new_movies.csv' AS row
CREATE (:Movie {
          id:toInteger(row.movieId),
          title:row.title,
          avgVote:toFloat(row.avgVote),
          releaseYear:toInteger(row.releaseYear),
          genres: split(row.genres,":") });

Data Transformation from string

 - to integer for releaseYear
 - to floating point for avgVote
 - to a list for genres

== Exercise: Import Person (10')  _(Normalized Data)_

- Import the Person from people.csv:
'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/people.csv'

- Determine the number of rows

- Determine structure of first 5 rows, watch out for spelling of headers!

*Goal:* CREATE :Person(id,name,born,died)

- Import people with CREATE
- Re-run with MERGE
- Make sure to transform the years.

NOTE: deathYear can be missing. Then toInt() returns null, which results in the property being skipped.

_Solution on next slide, don’t peek._

== _SOLUTION:_ Import Person _(Normalized Data)_

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/people.csv' as row
MERGE(person:Person {id: toInteger(row.personId)})
ON CREATE SET person.name = row.name,
              person.born = toInteger(row.birthYear),
              person.died = toInteger(row.deathYear)

deathYear can be missing. toInteger() will return null if field is missing, meaning the property is not set.

== Import Directors _(Normalized Data)_
//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/directors.csv' as row
MATCH (movie:Movie {id:toInteger(row.movieId)})
MATCH (person:Person {id: toInteger(row.personId)})
MERGE (person)-[:DIRECTED]->(movie)
ON CREATE SET person:Director

== Exercise: Import actors   _(Normalized Data)_

Create :ACTED_IN Relationship / :Actor Label

Discuss:

- FIELDTERMINATOR
- PERIODIC COMMIT
- Eagerness

*EXERCISE:*

From 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/actors.csv'

Suggestion: Determine rows and structure of first 5 entries

- Only create one ACTED_IN relationship per :Person, :Movie pair
- Set roles to a list of roles
- Set :Actor label

*_Solution on next page._*

== Solution: Import Actors  _(Normalized Data)_

//setup
//output
[source,cypher,subs=attributes]
USING PERIODIC COMMIT 50000
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/actors.csv' AS row
FIELDTERMINATOR ','
MATCH  (movie:Movie  {id: toInteger(row.movieId) })
MATCH  (person:Person {id: toInteger(row.personId) })
MERGE  (person)-[r:ACTED_IN]->(movie) ON CREATE SET r.roles = split(coalesce(row.characters,""), ":")
ON CREATE SET person:Actor

.Table Pro's and Con's of Normalized data import
[width=50%,options="header"cols=".<a,.<a"]
|====
| Pro's | Con's

|

 - Simple statements
 - Single merge for movies and actors
 - Single Pass
 |- Additional index lookups
 |- Deadlocks for relationships if parallelized
|====

== Denormalized Data (version 1)

 - Single CSV file
 - *_Multi-Pass_*
 - Create nodes individually, one per label
 - Create relationships, one per type

Same as previous exercises, just run multiple passes over the same file.

.Table Pro's and Con's of Normalized data import version 1

[width=50%,options="header"cols=".<a,.<a"]
|====
| Pro's | Con's

|
- Simple statement, easy to understand

|
- Unnecessary merges for duplicate movies and actors
- Additional index lookups
- Multi Pass
- Potential deadlocks for relationships if parallelized

|====

*NO EXAMPLE EXERCISES TO DO*

== Denormalized Data (version 2)

Using single denormalized CSV files

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM
     'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/movies.csv' AS row
RETURN count(*)

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM
     'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/movies.csv' AS row
RETURN row LIMIT 10

== Denormalized Data (version 2)

- Single-Pass

- Create sub-graph per row, e.g. Movie and Person and Relationship
//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/actors.csv' AS row
MERGE (m:Movie {id:toInteger(row.movieId)})
   ON CREATE SET m.title=row.title, m.avgVote=toFloat(row.avgVote),
      m.releaseYear=toInteger(row.releaseYear), m.genres=split(row.genres,":")
MERGE (p:Person {id: toInteger(row.personId)})
   ON CREATE SET p.name = row.name, p.born = toInteger(row.birthYear),
      p.died = toInteger(row.deathYear)
MERGE (p)-[:ACTED_IN {roles: split(coalesce(row.characters,""), ":")}]->(m)
ON CREATE SET p:Actor;

.Table Pro's and Con's of Normalized data import version 2

[width=50%,options="header"cols=".<a,.<a"]
|====
| Pro's
| Con's
|
- Saves index lookups
- Single Pass
- Works well with cost based (community) planner
|- More complex statement
- Unnecessary merges for duplicate movies and actors
- Deadlocks if parallelized
- Potential Eagerness problem
|====

 *END OF EXERCISES*

== Reduce Index lookups

- Small datasets (<1M) also work *_without_* PERIODIC COMMIT. Test it.
- Use distinct with input data, can use CREATE instead of MERGE
- MERGE has fewer lookups

//setup
//output
[source,cypher,subs=attributes]
LOAD CSV WITH HEADERS FROM
    'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/movies.csv' AS row
WITH DISTINCT toInteger(row.movieId) as movieId, row.title as title, row.genres as genres,
toInteger(row.releaseYear) as releaseYear, toFloat(row.avgVote) as avgVote
MERGE (m:Movie {id:movieId})
   ON CREATE SET m.title=title, m.avgVote=avgVote,
      m.releaseYear=releaseYear, m.genres=split(genres,":")

== Recovering if you messed up (with Periodic Commit)
- Mark newly created data with label (relationships with property) in (ON) CREATE
- Remove nodes with that label / rels with that property
- With Cypher-Shell (command line) use begin/rollback transactions

== Aggregate sub-structure
- Reduce Index-Lookup for Movie
- Turns statement to be (artificially) eager
- Effectively disables periodic commit (_potentially problematic_)

//setup
//output
[source,cypher,subs=attributes]
PROFILE
LOAD CSV WITH HEADERS FROM
     'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/movies.csv' AS row
WITH row.movieId as movieId, row.title as title, row.genres as genres,
toInteger(row.releaseYear) as releaseYear, toFloat(row.avgVote) as avgVote,
collect({id: row.personId, name:row.name, born: toInteger(row.birthYear), died:toInteger(row.deathYear),
         roles: split(coalesce(row.characters,""),':')}) as people
RETURN * LIMIT 10;

//setup
//output
[source,cypher,subs=attributes]
PROFILE
LOAD CSV WITH HEADERS FROM
     'https://raw.githubusercontent.com/neo4j-contrib/training/master/advanced_cypher/data/movies.csv' AS row
WITH row.movieId as movieId, row.title as title, row.genres as genres,
toInteger(row.releaseYear) as releaseYear, toFloat(row.avgVote) as avgVote,
collect({id: row.personId, name:row.name, born: toInteger(row.birthYear), died:toInteger(row.deathYear),
         roles: split(coalesce(row.characters,""),':')}) as people
MERGE (m:Movie {id:movieId})
   ON CREATE SET m.title=title, m.avgVote=avgVote,
      m.releaseYear=releaseYear, m.genres=split(genres,":")
WITH *
UNWIND people as person
MERGE (p:Person {id: person.id})
   ON CREATE SET p.name = person.name, p.born = person.born, p.died = person.died
CREATE (p)-[:ACTED_IN {roles: person.roles}]->(m)

== Extract Genre as a Node

//setup
//output
[source,cypher,subs=attributes]
CREATE CONSTRAINT ON (g:Genre) ASSERT g.name IS UNIQUE

//setup
//output
[source,cypher,subs=attributes]
MATCH (m:Movie)
UNWIND m.genres as name
WITH distinct name
CREATE (:Genre {name:name})

== Link :Genre to a :Movie

//setup
//output
[source,cypher,subs=attributes]
MATCH (m) WHERE not exists ((m)-[:GENRE]->())
WITH m LIMIT 10000
UNWIND m.genres as genre
MATCH (g:Genre {name: genre})
CREATE (m)-[:GENRE]->(g)
RETURN count(*);

See how many Genre's we created
MATCH (g:Genre)<-[r:GENRE]-()
return DISTINCT g.name AS Genre, count(r) AS count
ORDER BY count DESC
