= Creating Indexes

:presenter: Neo Technology
:twitter: neo4j
:email: info@neotechnology.com
:neo4j-version: 4.0
:currentyear: 2020
:doctype: book
:toc: left
:toclevels: 3
:prevsecttitle: Defining Constraints for your Data
:prevsect: 13
:currsect: 14
:nextsecttitle: Using Query Best Practices
:nextsect: 14
:experimental:
:imagedir: ../img
//:imagedir: https://s3-us-west-1.amazonaws.com/data.neo4j.com/intro-neo4j/img
:manual: http://neo4j.com/docs/developer-manual/current
:manual-cypher: {manual}/cypher

ifdef::backend-html5[]

include::scripts.txt[]

endif::backend-html5[]

== About this module

You have learned how to define both uniqueness and existence constraints for your data.
A uniqueness constraint is also an index where the default implementation is a b-tree structure.
Next, you will learn about creating property and fulltext schema indexes in the graph.

At the end of this module, you should be able to:
[square]

* Describe when indexes are used in Cypher
* Create a single property index
* Create a multi-property index
* Create a fulltext schema index
* Use a fulltext schema index
* Manage indexes
** List indexes
** Drop an index
** Drop a fulltext schema index

== Indexes in Neo4j

The uniqueness and node key constraints that you add to a graph are essentially single-property and composite indexes respectively.
Indexes are used to improve initial node lookup performance, but they require additional storage in the graph to maintain and also add to the cost of creating or modifying property values that are indexed.
Indexes store redundant data that points to nodes with the specific property value or values.
Unlike SQL, there is no such thing as a primary key in Neo4j.
You can have multiple properties on nodes that must be unique.

Here is a brief summary of when single-property indexes are used:

* Equality checks `=`
* Range comparisons `>`,`+>=+`,`<`, `+<=+`
* List membership `IN`
* String comparisons `STARTS WITH`, `ENDS WITH`, `CONTAINS`
* Existence checks `exists()`
* Spatial distance searches `distance()`
* Spatial bounding searches `point()`

Composite indexes are used only for equality checks and list membership.

In this course, we introduce the basics of Neo4j b-tree indexes, but you should consult the Neo4j _Operations Manual_ for more details about creating and maintaining indexes.

[NOTE]
Because index maintenance incurs additional overhead when nodes are created, Neo4j recommends that for large graphs, indexes are created after the data has been loaded into the graph.

=== Indexes for range searches

When you add an index for a property of a node, it can greatly reduce the number of nodes the graph engine needs to visit in order to satisfy a query.

In this query we are testing the value of the _released_ property of a _Movie_ node using ranges:
[source,cypher]
----
MATCH (m:Movie)
WHERE 1990 < m.released < 2000
SET m.videoFormat = 'DVD'
----

If there is an index on the _released_ property, the graph engine will find the pointers to all nodes that satisfy the query without having to visit all of the nodes:

image::{imagedir}/IndexForRanges.png[IndexForRanges,width=600,align=center]

== Creating a single-property index

You create an index to improve graph engine performance.
A uniqueness constraint on a property is an index so you need not create an index for any properties you have created uniqueness constraints for.
An index on its own does [.underline]#not# guarantee uniqueness.

Here is an example of how we create a single-property index on the _released_ property of all nodes of type _Movie_:
[source,cypher]
----
CREATE INDEX MovieReleased FOR (m:Movie) ON (m.released)
----

Notice that just as for constraints, a best practice is to specify a name for the index.
In this case, the name is _MovieReleased_.

With the result:
[.thumb]
image::{imagedir}/CreateSingle-propertyIndex.png[CreateSingle-propertyIndex,width=600]

== Creating a composite index

If a set of properties for a node must be unique for every node, then you should create a constraint as a node key, rather than an index.

If, however, there can be duplication for a set of property values, but you want faster access to them, then you can create a composite index.
A composite index is based upon multiple properties for a node.

Suppose we added the property, _videoFormat_ to every _Movie_ node and set its value, based upon the released date of the movie as follows:
[source,cypher]
----
MATCH (m:Movie)
WHERE m.released >= 2000
SET m.videoFormat = 'DVD';
MATCH (m:Movie)
WHERE m.released < 2000
SET m.videoFormat = 'VHS'
----

With the result:
[.thumb]
image::{imagedir}/TwoStatements.png[TwoStatements,width=900]

[NOTE]
Notice that in the above Cypher statements we use the semi-colon `;` to separate Cypher statements.
In general, you need not end a Cypher statement with a semi-colon, but if you want to execute multiple Cypher statements, you must separate them. You have already used the semi-colon to separate Cypher statements when you loaded the _Movie_ database in the training exercises.

Now that the graph has _Movie_ nodes with both the properties, _released_ and _videoFormat_, we can create a composite index on these properties as follows:
[source,cypher]
----
CREATE INDEX MovieReleasedVideoFormat FOR (m:Movie) ON (m.released, m.videoFormat)
----

With the result:
[.thumb]
image::{imagedir}/CreateCompositeIndex.png[CreateCompositeIndex,width=500]

== Creating fulltext schema indexes

A fulltext schema index is based upon string values only, but they provide additional search capabilities that you do not get from property indexes.
A fulltext schema index can be used for:

[square]
* Node or relationship properties.
* Single property or multiple properties.
* Single or multiple types of nodes (labels).
* Single or multiple types of relationships.

Rather than using Cypher syntax to create a fulltext schema index, you call a procedure to create the index.
The index is not used implicitly when you query the graph.
You must call a procedure to start a query that uses the index.
By default, the underlying implementation of a fulltext schema index is Lucene.
You can change the underlying index provider of any index.

Here is an example where we create a fulltext schema index on data in _title_ property of Movie nodes and data in the _name_ property of Person nodes:

[source,cypher]
----
CALL db.index.fulltext.createNodeIndex('MovieTitlePersonName',['Movie', 'Person'], ['title', 'name'])
----

The result returned shows nothing exceptional:
[.thumb]
image::{imagedir}/CreateFullTextIndex1.png[CreateFullTextIndex1,width=900]

After creating a fulltext schema index, you can always get of listing of all existing indexes:

[source,cypher]
----
CALL db.index.fulltext.createNodeIndex('MovieTitlePersonName',['Movie', 'Person'], ['title', 'name'])
----

And here we see our newly-created fulltext schema index:
[.thumb]
image::{imagedir}/CreateFullTextIndex2.png[CreateFullTextIndex2,width=900]

Just as you can create a fulltext schema index on properties of nodes, you can create a fulltext schema index on properties of relationships.
To do this you use `CALL db.indexfulltext.createRelationshipIndex()`.

== Using a fulltext schema index

To use a fulltext schema index, you must call the query procedure that uses the index.

Here is an example where we want to find all movies and person names that contain the string _Jerry_:

[source,cypher]
----
CALL db.index.fulltext.queryNodes('MovieTitlePersonName', 'Jerry') YIELD node
RETURN node
----

Notice that we specify `YIELD` after calling the procedure. This enables us to use return values from the procedure.
In this case, we return all nodes that are found in the graph that have either a _title_ property or _name_ property containing the string, _Jerry_.

And here is the result:
[.thumb]
image::{imagedir}/UseFullTextIndex1.png[UseFullTextIndex1,width=900]

When a fulltext schema index is used, it calculates a "hit score" that represents the closeness of the values in the graph to the query string.

Here is an example:

[source,cypher]
----
CALL db.index.fulltext.queryNodes('MovieTitlePersonName', 'Matrix') YIELD node, score
RETURN node.title, score
----

The nodes returned have a Lucene score based upon how much of _Matrix_ was part of the _title_:
[.thumb]
image::{imagedir}/UseFullTextIndex2.png[UseFullTextIndex2,width=900]

With fulltext indexes created, you can also specify which property you want to search for. Here is an example where we are looking for _Jerry_, but only as a _name_ property of a Person node:

[source,cypher]
----
CALL db.index.fulltext.queryNodes('MovieTitlePersonName', 'name: Jerry') YIELD node
RETURN node
----

Here is what is returned:
[.thumb]
image::{imagedir}/UseFullTextIndex3.png[UseFullTextIndex3,width=900]

Please see the _Cypher Reference Manual_ for more on using fulltext schema indexes.

== Managing Indexes

You have already see the three types of indexes in our database thus far using this Cypher statement:

[source,cypher]
----
CALL db.indexes()
----

Here is what is returned:
[.thumb]
image::{imagedir}/ManagingIndexes1.png[ManagingIndexes1,width=900]

To drop an index on a property, you simply use the `DROP INDEX` clause, specifying the name of the index:

[source,cypher]
----
DROP INDEX MovieReleasedVideoFormat
----

With the result:
[.thumb]
image::{imagedir}/ManagingIndexes2.png[ManagingIndexes2,width=900]

To drop a fulltext schema index, you must call the procedure. Here we drop the index that we created earlier:


[source,cypher]
----
CALL db.index.fulltext.drop('MovieTitlePersonName')
----

With the result:
[.thumb]
image::{imagedir}/ManagingIndexes3.png[ManagingIndexes3,width=900]

You must list the indexes to confirm that it was dropped:

[source,cypher]
----
CALL db.indexes()
----

Here is what is returned:
[.thumb]
image::{imagedir}/ManagingIndexes4.png[ManagingIndexes4,width=900]

== *Exercise 14: Creating indexes*

In the query edit pane of Neo4j Browser, execute the browser command: kbd:[:play 4.0-intro-neo4j-exercises]
and follow the instructions for Exercise 14.

[#module-7.quiz]
== Check your understanding
=== Question 1

What Cypher code below will create a unique index on the _name_ property of the Person node?

Select the correct answer.
[%interactive]

- [ ] [.false-answer]#`CREATE INDEX PersonNameIndex FOR (p:Person) ON (p.name)``#
- [ ] [.false-answer]#`CREATE INDEX PersonNameIndex FOR (p:Person) ON (p.name) ASSERT p.name IS UNIQUE`#
- [ ] [.required-answer]#`CREATE CONSTRAINT PersonNameConstraint ON (p:Person) ASSERT p.name IS UNIQUE`#
- [ ] [.false-answer]#`CALL db.index.fulltext.createNodeIndex('PersonName',['Person'], ['name'])``#


=== Question 2

What makes creating a fulltext schema index different from creating a property index?

Select the correct answers.
[%interactive]

- [ ] [.required-answer]#Fulltext schema indexes can use relationship properties.#
- [ ] [.false-answer]#Fulltext schema indexes can check for uniqueness.#
- [ ] [.required-answer]#Fulltext schema indexes can use multiple types of nodes for the index.#
- [ ] [.false-answer]#Fulltext schema indexes can be used to ensure the existence of a property.#

=== Question 3

What is the difference between a node key and a composite index?

Select the correct answer.
[%interactive]
- [ ] [.false-answer]#A composite index can utilize more than one type of node.#
- [ ] [.false-answer]#A composite index can use relationship properties.#
- [ ] [.required-answer]#A composite index does not enforce uniqueness.#
- [ ] [.false-answer]#A composite index can enforce existence.#

== Summary

You should now be able to:
[square]

* Describe when indexes are used in Cypher
* Create a single property index
* Create a multi-property index
* Create a fulltext schema index
* Use a fulltext schema index
* Manage indexes
** List indexes
** Drop an index
** Drop a fulltext schema index

++++
<a class="next-section medium button" href="../part-8/">Continue to Module 8</a>
++++

ifdef::backend-html5[]

include::scripts-end.txt[]

++++
<script>
$( document ).ready(function() {
  Intercom('trackEvent','training-introv2-view-part7');
});
</script>
++++

endif::backend-html5[]
