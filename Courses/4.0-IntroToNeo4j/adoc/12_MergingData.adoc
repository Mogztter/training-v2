
= Merging Data

:presenter: Neo Technology
:twitter: neo4j
:email: info@neotechnology.com
:neo4j-version: 4.0
:currentyear: 2020
:doctype: book
:toc: left
:toclevels: 3
:prevsecttitle: Creating Relationships
:prevsect: 11
:currsect: 12
:nextsecttitle: Merging Data
:nextsect: 13
:experimental:
:imagedir: ../img
//:imagedir: https://s3-us-west-1.amazonaws.com/data.neo4j.com/intro-neo4j/img
:manual: http://neo4j.com/docs/developer-manual/current
:manual-cypher: {manual}/cypher

ifdef::backend-html5[]

include::scripts.txt[]

endif::backend-html5[]

== About this module

You have learned how to create nodes and relationships in the graph.
When you create a node you want to ensure that there is only one instance of that node in the graph.
You also want to ensure that you do not create unnecessary or duplicate relationships in the graph.
Next, you will learn how to merge data in the graph.

At the end of this module, you should be able to write Cypher statements to:
[square]
* Merge data in a graph
** Creating nodes
** Creating relationships

== Merging data in the graph

Thus far, you have learned how to create nodes, labels, properties, and relationships in the graph.
You can use `MERGE` to either create new nodes and relationships or to make changes to existing nodes and relationships.

For example, how the graph engine behaves when a duplicate element is created depends on the type of element.
You have not yet learned about defining constraints in the graph.
Constraints can be used to eliminate duplication of nodes and you will learn about them later in this course.

Here are some facts about creating nodes and relationships in the graph when no constraints have been defined.

{set:cellbgcolor:white}
[frame="none",grid=none,width="90%"cols="35,65",stripes=none]
|===
h|If you use `CREATE`:
h|The result is:
|Node
|If a node with the same property values exists, a duplicate node is created.
|Label
|If the label already exists for the node, the node is not updated.
|Property
|If the node or relationship property already exists, it is updated with the new value.  
// force line break
{nbsp} + 
*Note:* If you specify a set of properties to be created using `=` rather than `+=`, it could remove existing properties if they are not included in the set.
|Relationship
|If the relationship exists, a duplicate relationship is created.
|===
{set:cellbgcolor!}

[WARNING]
You should never create duplicate nodes or relationships in a graph.

The `MERGE` clause is used to find nodes or patterns in the graph.
If the node or pattern is not found, by default, it is created.

You use the simple `MERGE` clause to:

[square]
* Create a unique node based on label and key information for a property or set of properties,
* Update a node based on label and key information for a property or set of properties.
* Create a unique relationship between two nodes.
* Create a unique node and relationship in the context of another node.

=== Using `MERGE` to create nodes

Here is the simplified syntax for the `MERGE` clause for creating a node:
[source,cypher]
----
MERGE (variable:Label{nodeProperties})
RETURN variable
----

If there is an existing node with  _Label_ and _nodeProperties_  found in the graph, no node is created.
If, however the node is not found in the graph, then the node is created.

When you specify _nodeProperties_ for `MERGE`, you should only use properties that satisfy some sort of uniqueness constraint.
You will learn about uniqueness constraints later in this course.

Here is what we currently have in the graph for the _Person_, _Michael Caine_. 
This node has values for _name_ and _born_. Notice also that the label for the node is _Person_.

[source,cypher]
----
MATCH (p:Person)
WHERE p.name = 'Michael Caine'
RETURN p
----

[.thumb]
image::{imagedir}/MichaelCaine.png[MichaelCaine,width=600]

In this example, we use `MERGE` to find a node with the _Actor_ label with the key property _name_ of _Michael Caine_, and we set the _born_ property to _1933_. Our data model has never used the label, _Actor_ so this is a new entity type in our graph.

[source,cypher]
----
MERGE (a:Actor {name: 'Michael Caine'})
SET a.born = 1933
RETURN a
----

Here is the result of running this Cypher example.
We do not find a node with the label _Actor_ so the graph engine creates one.

[.thumb]
image::{imagedir}/MergeActorMichaelCaine.png[MergeActorMichaelCaine,width=600]

[NOTE]
A best practice when using `MERGE` is to only specify properties that have unique values and unique labels.

If we were to repeat this `MERGE` clause, no additional _Actor_ nodes would be created in the graph.

At this point, however, we have two _Michael Caine_ nodes in the graph, one of type _Person_, and one of type _Actor_:

[.thumb]
image::{imagedir}/TwoMichaelCaines.png[TwoMichaelCaines,width=800]

[NOTE]
Be mindful that node labels and the properties for a node are significant when merging nodes.

If we were to run `MERGE` code again:

[source,cypher]
----
MERGE (a:Actor {name: 'Michael Caine'})
SET a.born = 1933
WITH a
MATCH (p)
WHERE p.name = 'Michael Caine'
RETURN p
----

We would find that the Michael Caine node with the label Actor is not created.
The `MERGE` found this node in the graph and did not create a new one

[.thumb]
image::{imagedir}/OnlyTwoMichaelCaines.png[OnlyTwoMichaelCaines,width=800]

== Using `MERGE` to create relationships

Here is the syntax for the `MERGE` clause for creating relationships:
[source,cypher]
----
MERGE (variable1:Label1 {nodeProperties1})-[:REL_TYPE]->(variable2:Label2 {nodeProperties2})
RETURN variable
----
If there is an existing node with  _Label1_ and _nodeProperties1_  with the _:REL_TYPE_ relationship to an existing node with  _Label2_ and _nodeProperties2_ in the graph, no relationship is created. If the relationship does not exist, the relationship is created.

Here is an example. We currently have the _Person_ node with the _:ACTED_IN_ relationship, but we do not have this relationship with the _Actor_ node.

[source,cypher]
----
MATCH (p {name: 'Michael Caine'})-[*0..1]-(m)
RETURN p, m
----

Here is the result:

[.thumb]
image::{imagedir}/MichaelCainesAndOneRel.png[MichaelCainesAndOneRel,width=800]

Here is code where we want to create the _:ACTED_IN_ relationship between Michael Caine and the movie Batman Begins.

[source,cypher]
----
MATCH (p {name: 'Michael Caine'}),(m:Movie {title:'Batman Begins'})
MERGE (p)-[:ACTED_IN]->(m)
RETURN p,m
----

Here is the result of running this code:
[.thumb]
image::{imagedir}/MergeToCreateRel.png[MergeToCreateRel,width=800]

Since the relationship between the _Person_ node and the _Movie_ node already exists, it is not created.
The relationship between the _Actor_ node and the _Movie_ node is created with this merge.

[NOTE]
Although, you can leave out the direction of the relationship being created with the `MERGE`, in which case a left-to-right arrow will be assumed, a best practice is to always specify the direction of the relationship. However, if you have  bidirectional relationships and you want to avoid creating duplicate relationships, you [.underline]#must# leave off the arrow.

== Specifying creation behavior when merging

You can use the `MERGE` clause, along with `ON CREATE` to assign specific values to a node being created as a result of an attempt to merge.

Here is an example where create a new node, specifying property values for the new node:

[source,cypher]
----
MERGE (a:Person {name: 'Sir Michael Caine'})
ON CREATE SET a.birthPlace = 'London',
              a.born = 1934
RETURN a
----

We know that there are no existing _Sir Michael Caine_ _Person_ nodes. When the `MERGE` executes, it will not find any matching nodes so it will create one and will execute the `ON CREATE` clause where we set the _birthplace_ and _born_ property values. 

Here is the result of executing this code:
[.thumb]
image::{imagedir}/CreateSirMichaelCaine.png[CreateSirMichaelCaine,width=800]

Here is the code to display the nodes that have anything to do with Michael Caine.

[source,cypher]
----
MATCH (p)-[*0..1]-(m)
WHERE p.name CONTAINS 'Caine'
RETURN p, m
----

The most recently created node has the _name_ value of _Sir Michael Caine_.

[.thumb]
image::{imagedir}/AllMichaelCaines.png[AllMichaelCaines,width=800]

== Specifying update behavior when merging

You can also specify an `ON MATCH` clause during merge processing.
If the exact node is found, you can update its properties or labels. Here is an example:

[source,cypher]
----
MERGE (a:Person {name: 'Sir Michael Caine'})
ON CREATE SET a.born = 1934, 
              a.birthPlace = 'UK'
ON MATCH SET a.birthPlace = 'UK'
----

And here we see that only the exiting node with the name, Sir Michael Caine is updated with the new _birthPlace_.
Furthermore, no new node is created for Sir Michael Caine.

[.thumb]
image::{imagedir}/UpdateToUK.png[UpdateToUK,width=700]

=== Using `MERGE` to create relationships

Using `MERGE` to create relationships is expensive and you should only do it when you need to ensure that a relationship is unique and you are [underline]#not# sure if it already exists.

In this example, we use the `MATCH` clause to find all _Person_ nodes that represent _Michael Caine_ and we find the movie, _Batman Begins_ that we want to connect to all of these nodes. We already have a connection between one of the _Person_ nodes and the _Movie_ node. We do not want this relationship to be duplicated. This is where we can use `MERGE` as follows:

[source,cypher]
----
MATCH (p:Person), (m:Movie)
WHERE m.title = 'Batman Begins' AND p.name ENDS WITH 'Caine'
MERGE (p)-[:ACTED_IN]->(m)
RETURN p, m
----

Here is the result of executing this Cypher statement. It went through all the nodes and added the relationship to the nodes that didn't already have the relationship.

[.thumb]
image::{imagedir}/MergeRelationships.png[MergeRelationships,width=800]

You must be aware of the  behavior of the `MERGE` clause and how it will automatically create nodes and relationships.
`MERGE` tries to find a full pattern and if it doesn't find it, it creates that full pattern.
That's why in most cases you should first `MERGE` your nodes and then your relationship afterwards.

Only if you intentionally want to create a node within the context of another (like a month within a year) then a MERGE pattern with one bound and one unbound node makes sense.

For example:
[source,cypher]
----
MERGE (fromDate:Date {year: 2018})<-[:IN_YEAR]-(toDate:Date {month: 'January'})
----

=== *Exercise 12: Merging data in the graph*

In the query edit pane of Neo4j Browser, execute the browser command: kbd:[:play 4.0-intro-neo4j-exercises]
and follow the instructions for Exercise 12.

[#module-6.quiz]
== Check your understanding
=== Question 1

Given this `MERGE` clause:

[source,cypher]
----
MERGE (p:Person {name: 'Jane Doe'})
SET p.born = 1990
RETURN p
----

What is the most important thing you should make sure of?

Select the correct answer.
[%interactive]

- [ ] [.false-answer]#The _Person_ label exists in the graph.#
- [ ] [.false-answer]#The _Person_ label does not exist in the graph.#
- [ ] [.required-answer]#The value for name is unique.#
- [ ] [.false-answer]#The value for born is unique.#


=== Question 2

Given this `MERGE` clause:

[source,cypher]
----
MATCH (p {name: 'Jane Doe'}),(m:Movie {title:'The Good One'})
MERGE (p)-[:ACTED_IN]->(m)
----

Suppose that the _p_ and _m_ nodes exist in the graph.
What does this code do?

Select the correct answers.
[%interactive]

- [ ] [.false-answer]#If the `:ACTED_IN` relationship exists, it deletes it and recreates it.#
- [ ] [.required-answer]#If the `:ACTED_IN` relationship exists, it does nothing.#
- [ ] [.required-answer]#If the `:ACTED_IN` relationship does not exist, it creates it.#
- [ ] [.false-answer]#If the `:ACTED_IN` relationship does not exist, it creates another one.#

=== Question 3

Given this `MERGE` clause:

[source,cypher]
----
MATCH (p {name: 'Jane Doe'}),(m:Movie {title:'The Good One'})
MERGE (p)-[rel:ACTED_IN]->(m)
----

Suppose that the _p_ and _m_ nodes exist in the graph, but you do not know whether the relationship exists.
What are your options to process this `MERGE` clause?

Select the correct answers.
[%interactive]

- [ ] [.required-answer]#Use the default behavior. The relationship will be created if it doesn't exist.#
- [ ] [.required-answer]#Specify `ON CREATE` to perform additional processing when the relationship is created.#
- [ ] [.required-answer]#Specify `ON MATCH` to perform additional processing when the relationship is not created.#
- [ ] [.false-answer]#Specify `ON DELETE` to perform additional processing when the relationship is deleted.#

== Summary

You should now be able to write Cypher statements to:
[square]
* Merge data in a graph
** Creating nodes
** Creating relationships

== Grade Quiz and Continue 

++++
<a class="next-section medium button" href="../part-7/">Continue to Module 7</a>
++++

ifdef::backend-html5[]

include::scripts-end.txt[]
++++
<script>
$( document ).ready(function() {
  Intercom('trackEvent','training-introv2-view-part6');
});
</script>
++++

endif::backend-html5[]
